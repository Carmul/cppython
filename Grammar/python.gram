# Implimented Python grammar

	
program : statements EOF_TOKEN

statements : ( compound_stmt | simple_stmt NEWLINE )*

simple_stmt : assignment_stmt | return_stmt | expr

compound_stmt : if_stmt | while_stmt | function_def

if_stmt : IF expr COLON NEWLINE block else_stmt?

elif_stmt : ELIF expr COLON NEWLINE block (else_stmt | elif_stmt)?

else_stmt : ELSE COLON NEWLINE block

while_stmt : WHILE expr COLON NEWLINE block

block : INDENT statements DEDENT

assignment_stmt : NAME EQUAL expr

function_def : DEF NAME LPAR (NAME (COMMA NAME)*)? RPAR COLON NEWLINE block

return_stmt : RETURN expr


expr : disjunction

disjunction : conjunction ( OR conjunction )*

conjunction : inversion ( AND inversion )*

inversion : comparison | NOT inversion

comparison : arith_expr ( (EQEQUAL | NOTEQUAL | LESSEQUAL | GREATEREQUAL | LESS | GREATER) arith_expr )*

arith_expr : term ( (PLUS | MINUS) term )*
	
term : factor ( (MUL | DIV | PERCENT) factor )*
	
factor : (PLUS | MINUS) factor | NUMBER | NAME | LPAR expr RPAR | BOOLEAN | STRING | function_call

function_call : NAME LPAR (expr (COMMA expr)*)? RPAR
	